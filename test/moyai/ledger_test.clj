(ns moyai.ledger-test
  (:require [clojure.test :refer [deftest is testing]]
            [moyai.ledger :as ledger]))

(def holder-a "did:example:a")
(def holder-b "did:example:b")

(deftest mint-burn-and-conservation
  (let [minted (ledger/mint [] holder-a 8 1 "contribution:a")
        burned (ledger/burn minted holder-a 2 1 "draw:a")]
    (is (= 8 (ledger/total-minted burned)))
    (is (= 2 (ledger/total-burned burned)))
    (is (= burned (ledger/assert-conservation burned)))
    (is (pos? (ledger/balance burned holder-a 1)))
    (is (zero? (ledger/balance burned holder-b 1)))))

(deftest decay-is-a-flow-not-a-store
  (let [log (ledger/mint [] holder-a 4 1 "contribution:a")]
    (is (< (abs (- 2.0
                   (ledger/balance
                    log holder-a (+ 1 ledger/half-life-epochs))))
           1e-9))))

(deftest monetary-governance-and-benefit-firewalls
  (is (zero? (ledger/redeemable-usd-micros)))
  (is (false? (ledger/grants-governance-weight?)))
  (is (false? (ledger/grants-benefit-or-stage?)))
  (doseq [forbidden '[transfer gift merge pool]]
    (is (not (contains? (ns-publics 'moyai.ledger) forbidden)))))

(deftest append-only-and-overdraw-gates
  (testing "epochs are monotone"
    (is (thrown? clojure.lang.ExceptionInfo
                 (ledger/mint
                  (ledger/mint [] holder-a 1 2 "a")
                  holder-a 1 1 "backdated"))))
  (testing "burn cannot exceed decayed balance"
    (is (thrown? clojure.lang.ExceptionInfo
                 (ledger/burn
                  (ledger/mint [] holder-a 1 1 "a")
                  holder-a 2 1 "overdraw"))))
  (testing "units are positive integers"
    (is (thrown? clojure.lang.ExceptionInfo
                 (ledger/mint [] holder-a 0 1 "zero")))))
